def artifactoryUrl = project.properties['kisio_artifactory_url'] ?: ""
def artifactoryUsername = project.properties['kisio_artifactory_username'] ?: ""
def artifactoryPassword = project.properties['kisio_artifactory_password'] ?: ""
def artifactoryAndroidRepoRelease = project.properties['kisio_artifactory_android_repo_release'] ?: ""
def artifactoryAndroidRepoSnapshot = project.properties['kisio_artifactory_android_repo_snapshot'] ?: ""

repositories {
    google()
    mavenLocal()
    maven {
        url("https://dl.bintray.com/navitiasdkteam/NavitiaSDK")
    }

    if (project.hasProperty("kisio_artifactory_url")) {
        maven {
            url(artifactoryUrl + artifactoryAndroidRepoRelease)
            credentials {
                username = artifactoryUsername
                password = artifactoryPassword
            }
        }
        maven {
            url(artifactoryUrl + artifactoryAndroidRepoSnapshot)
            credentials {
                username = artifactoryUsername
                password = artifactoryPassword
            }
        }
    }
    
    maven {
        url("https://maven.google.com")
    }
}

def supportLibraryVersion = "28.0.0"
def DEFAULT_MIN_SDK_VERSION = 19
def minSdk = Math.max(DEFAULT_MIN_SDK_VERSION, cdvHelpers.getConfigPreference('android-minSdkVersion', 0) as Integer)
if (cdvMinSdkVersion == null || Integer.parseInt('' + cdvMinSdkVersion) < minSdk ) {
    ext.cdvMinSdkVersion = minSdk;
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex") && !requested.name.contains("support-core-ui") && !requested.name.contains("support-compat")) {
                details.useVersion(supportLibraryVersion)
            }
        }
    }
}

android {
    defaultConfig {
        multiDexEnabled = true
    }
}

ext.postBuildExtras = {
    android {
        allprojects {
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }
    }
}

dependencies {
    implementation("org.kisio.sdk:navitia-sdk-ui:2.1.0")
    implementation("com.android.support:appcompat-v7:$supportLibraryVersion")
    implementation("com.android.support:design:$supportLibraryVersion")
}
